#!/usr/bin/env zsh

# Makes sure that all current dart rules are assigned in your
# analysis_options.yaml (so your config can stay up to date).

# Note that this script assumes the following:
#	1)	You're using key value syntax in your yaml instead of list syntax (see
#		https://dart.dev/tools/analysis#disabling-individual-rules) and
#	2)	You're listing *every available rule* (that's not marked experimental or
#		deprecated) with a true/false value, ensuring that you've made a
#		conscious decision on each

# Make sure yq is installed
which yq &> /dev/null || {
	echo "Please install yq" >&2
	exit 1
}

# Make sure jq is installed
which jq &> /dev/null || {
	echo "Please install jq" >&2
	exit 1
}

# Assign argument
f=$1

# Make sure file is named analysis_options.yaml (rigid, but safe)
wants=analysis_options.yaml
[ $(basename $f) = $wants ] || {
	echo "Please supply a file called $wants." >&2
	exit 1
}

# Make sure file exists
[ -f $f ] || {
	echo "$f does not exist." >&2
	exit 1
}

# Pull description list of rules for later checks
descList=$(curl -s https://dart.dev/tools/linter-rules)

# Set default exit code to 0
exitCode=0

# Pull in master list and start to loop through all linter rules
curl -s https://dart.dev/tools/linter-rules/all | \
	xmllint --html --xpath "string(/html/body//div[contains(@class,'code-block-body')]//code)" - \
	2> /dev/null | \
	yq -r -o json ".linter.rules" | \
	jq -r ".[]" | \
while read -r allRule
do
	# Check whether rule exists in config
	isInConfig=$(
		cat $f | \
			yq -o json ".linter.rules" | \
			jq -r "has(\"$allRule\")"
	)
	# Continue if rule is in config
	[ $isInConfig = true ] && continue
	# Check whether rule is deprecated or experimental and continue if so
	isDeprecatedOrExperimental=$(
		echo $descList | \
			xmllint --html --xpath "//a[@href=\"/tools/linter-rules/$allRule\"]/.." - \
			2> /dev/null | \
			grep -Eic "\((experimental|deprecated|unreleased)\)"
	)
	[ $isDeprecatedOrExperimental -gt 0 ] && continue
	# Set exit code to 1
	exitCode=1
	# Notify
	echo -e "\033[1;31mMissing rule:\033[0m https://dart.dev/tools/linter-rules/\033[36m$allRule\033[0m" >&2
done

# Exit
exit $exitCode
